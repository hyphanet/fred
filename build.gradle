buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
        classpath files('gradle/gradle-witness.jar')
    }
}

configurations {
    provided
    compile
}

apply plugin: 'witness'
apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    flatDir { dirs uri("${projectDir}/lib") }
    maven { url 'https://mvn.freenetproject.org' }
    mavenCentral()
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
}

def version_src = 'freenet/node/Version.java'

sourceSets {
    main {
        java {
            srcDir 'src/'
        }
    }
    test {
        java {
            srcDir 'test/'
        }
    }
}

def gitrev
task buildInfo {
    try {
        def cmd = "git describe --always --abbrev=4 --dirty"
        def proc = cmd.execute()
        gitrev = proc.text.trim()
    } catch (java.io.IOException e) {
        gitrev = "@unknown@"
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc) {
    options.encoding = "UTF-8"
}

task compileVersion (type: JavaCompile) {
    copy {
        from sourceSets.main.java.srcDirs
        into "${buildDir}/build/tmp/compileVersion/"
        include "${version_src}"
        filter {
            String line -> line.replaceAll("@custom@","${gitrev}")
        }
    }
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    source = "${buildDir}/build/tmp/compileVersion/"
    include "${version_src}"
    classpath = files(sourceSets.main.compileClasspath, sourceSets.main.output.classesDir)
    destinationDir = file("${buildDir}/java/version/")
}

compileVersion.dependsOn buildInfo
compileVersion.dependsOn compileJava
processResources.dependsOn compileVersion

task jar (type: Jar, overwrite: true) {
    from (compileVersion) {
        include 'freenet/node/Version**class'
    }
    from ("${buildDir}/classes/java/main/") {
        exclude 'freenet/node/Version.class'
        exclude 'freenet/node/Version$1.class'
    }
    preserveFileTimestamps = true
    reproducibleFileOrder = true
    duplicatesStrategy = "warn"
    archivesBaseName = "freenet"
    manifest {
        attributes("Permissions": "all-permissions")
        attributes("Application-Name": "Freenet REference Daemon")
        attributes("Required-Ext-Version": 29)
        attributes("Recommended-Ext-Version": 29)
        attributes("Compiled-With": "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})")
        attributes([
                       "Specification-Title": "Freenet",
                       "Specification-Version": "0.7.5",
                       "Specification-Vendor": "freenetproject.org",
                       "Implementation-Title": "Freenet",
                       "Implementation-Version": "0.7.5 ${gitrev}",
                       "Implementation-Vendor": "freenetproject.org",
                   ], "common")
    }
}
jar.dependsOn processResources

def jars = []
gradle.addListener(new TaskExecutionListener() {
void afterExecute(Task task, TaskState state) {
    if(task in AbstractArchiveTask) {
        jars << task.outputs.files.singleFile
    }
}

void beforeExecute(Task task) { }
})
gradle.addBuildListener(new BuildAdapter() {
    void buildFinished(BuildResult result) {
        if(jars) {
            def hash = {
                File file -> def sha256 = java.security.MessageDigest.getInstance('SHA-256')
                file.eachByte(1024 * 4) { buffer, len -> sha256.update(buffer, 0, len) }
                println "SHA-256 of ${file.name}: ${sha256.digest().encodeHex().toString()}"
            }

            jars.each { hash(it) }
        }
    }
})

task copyResourcesToClasses2 {
    inputs.dir sourceSets.main.java.srcDirs
    outputs.dir sourceSets.main.output.classesDir
    doLast {
        copy {
            from sourceSets.main.java.srcDirs
            into sourceSets.main.output.classesDir
            include 'freenet/l10n/*properties'
            include 'freenet/l10n/iso-*.tab'
            include 'freenet/clients/http/staticfiles/**'
            include 'freenet/clients/http/templates/**'
            include '../dependencies.properties'
        }
        copy {
            from "${projectDir}/"
            into sourceSets.main.output.classesDir
            include 'dependencies.properties'
        }
    }
}
processResources.dependsOn copyResourcesToClasses2

task copyTestResourcesToClasses2 {
    inputs.dir sourceSets.test.java.srcDirs
    outputs.dir sourceSets.test.output.classesDir
    doLast {
        copy {
	    from sourceSets.test.java.srcDirs
	    into sourceSets.test.output.classesDir
            include 'freenet/client/filter/*/**'
            include 'freenet/crypt/ciphers/rijndael-gladman-test-data/**'
            include 'freenet/l10n/*properties'
            include 'freenet/clients/http/templates/**'
        }
    }
}
compileTestJava.dependsOn copyResourcesToClasses2
compileTestJava.dependsOn copyTestResourcesToClasses2

test {
    if(JavaVersion.current() >= JavaVersion.VERSION_1_9) {
        jvmArgs '--add-opens=java.base/java.lang=ALL-UNNAMED'
        jvmArgs '--add-opens=java.base/java.util=ALL-UNNAMED'
        jvmArgs '--add-opens=java.base/java.io=ALL-UNNAMED'
        jvmArgs '--add-opens=java.base/java.util.zip=ALL-UNNAMED'
    }
    minHeapSize = "128m"
    maxHeapSize = "512m"
    // no inner class
    include 'freenet/**/*Test.class'
    exclude 'freenet/**/*$*Test.class'
    workingDir = sourceSets.test.output.classesDir
    scanForTestClasses = false
    systemProperties += [
        "test.l10npath_test": "freenet/l10n/",
        "test.l10npath_main": "../main/freenet/l10n/"
//	"test.extensive":
//	"test.verbose":
//	"test.benchmark":
    ]
}

tasks.withType(Test) {
    enableAssertions = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'org.freenetproject'
            artifactId "fred"
            version gitrev
            from components.java
        }
    }
    repositories {
        maven {
            url "s3://mvn.freenetproject.org/"
            credentials(AwsCredentials) {
                accessKey System.getenv('AWS_ACCESS_KEY_ID')
                secretKey System.getenv('AWS_SECRET_ACCESS_KEY')
            }
        }
    }
}

task copyRuntimeLibs(type: Copy) {
    into "${buildDir}/output/"
    from configurations.runtime
    from jar
}
copyRuntimeLibs.dependsOn jar

// In this section you declare the dependencies for your production and test code
dependencies {
    compile "org.bouncycastle:bcprov-jdk15on:1.59"
    compile "net.java.dev.jna:jna:4.5.2"
    compile "net.java.dev.jna:jna-platform:4.5.2"
    compile "org.freenetproject:freenet-ext:29"
    compile "io.pebbletemplates:pebble:3.1.5"
    // dependencies of pebble
    compile "org.unbescape:unbescape:1.1.6.RELEASE"
    compile "org.slf4j:slf4j-api:1.7.25"

    testCompile 'junit:junit:4.13.2'
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile "org.hamcrest:hamcrest:2.2"
    testCompile "org.objenesis:objenesis:1.0"
}

dependencyVerification {
    // testCompile includes all of compile deps... so let's include only these
    includedConfigurations = [configurations.testCompile]
    verify = [
        'org.bouncycastle:bcprov-jdk15on:1c31e44e331d25e46d293b3e8ee2d07028a67db011e74cb2443285aed1d59c85',
        'net.java.dev.jna:jna-platform:f1d00c167d8921c6e23c626ef9f1c3ae0be473c95c68ffa012bc7ae55a87e2d6',
        'net.java.dev.jna:jna:0c8eb7acf67261656d79005191debaba3b6bf5dd60a43735a245429381dbecff',
        'org.freenetproject:freenet-ext:32f2b3d6beedf54137ea2f9a3ebef67666d769f0966b08cd17fd7db59ba4d79f',
        'junit:junit:8e495b634469d64fb8acfa3495a065cbacc8a0fff55ce1e31007be4c16dc57d3',
        'org.mockito:mockito-core:f97483ba0944b9fa133aa29638764ddbeadb51ec3dbc02074c58fa2caecd07fa',
        'org.hamcrest:hamcrest:5e62846a89f05cd78cd9c1a553f340d002458380c320455dd1f8fc5497a8a1c1',
        'org.objenesis:objenesis:c5694b55d92527479382f254199b3c6b1d8780f652ad61e9ca59919887f491a8',
        'io.pebbletemplates:pebble:d253a6dde59e138698aaaaee546461d2f1f6c8bd2aa38ecdd347df17cf90d6f0',
        // dependencies of pebble
        'org.unbescape:unbescape:597cf87d5b1a4f385b9d1cec974b7b483abb3ee85fc5b3f8b62af8e4bec95c2c',
        'org.slf4j:slf4j-api:18c4a0095d5c1da6b817592e767bb23d29dd2f560ad74df75ff3961dbde25b79'
    ]
}

task tar(type: Tar) {
  description = "Build a source release, specifically excluding the build directories and gradle wrapper files"
  compression = Compression.BZIP2

  baseName = "freenet-sources"

  from(project.rootDir) {
    exclude '**/build'
    exclude 'build'
    exclude '.gradle'
  }

  into(baseName)

  preserveFileTimestamps = true
  reproducibleFileOrder = true

  // Set destination directory.
  destinationDir = file("${project.buildDir}")

  archiveName = "${baseName}.tgz"
  doLast { // generate md5 checksum
    ant.checksum file:"$destinationDir/$archiveName"
  }
}

javadoc << {
    failOnError false
}

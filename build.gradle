buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
        classpath files('lib/gradle-witness.jar')
    }
}

configurations {
    provided
    compile
}

apply plugin: 'witness'
apply plugin: 'java'

repositories {
    jcenter()
    mavenLocal() // TODO: use lib/ instead?
    ivy {
        url 'https://downloads.freenetproject.org/'
        layout 'pattern', {
            artifact '/latest/[module][revision].[ext]'
        }
    }
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
}

sourceSets {
    main {
        java {
            srcDir 'src/'
        }
    }
    test {
        java {
            srcDir 'test/'
        }
    }
}

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    archivesBaseName = "freenet"
}

def gitrev
task buildInfo {
    try {
        def cmd = "git describe --always --abbrev=4 --dirty"
        def proc = cmd.execute()
        gitrev = proc.text.trim()
    } catch (java.io.IOException e) {
        gitrev = "@unknown@"
    }
}

// This isn't used anywhere yet
// I am not sure of whether we should parse and use the info here or generate the file!
def classpathDependencies = []
task parseManifest {
    Properties properties = new Properties()
    new File('dependencies.properties').withInputStream {
        properties.load(it)
    }
    for (String s in properties.stringPropertyNames().findAll {
    it.endsWith(".type") && properties.getProperty(it) == "CLASSPATH" }) {
        def dep = s.split("\\.")[0]
        def dep_sha256 = properties.getProperty("${dep}.sha256")
        def dep_version = properties.getProperty("${dep}.version")
        def dep_filename = properties.getProperty("${dep}.filename")
	classpathDependencies.add([ dep, dep_filename, dep_version, dep_sha256 ])
    }
}

jar {
    manifest {
        attributes("Permissions": "all-permissions")
        attributes("Application-Name": "Freenet REference Daemon")
        attributes("Required-Ext-Version": 29)
        attributes("Recommended-Ext-Version": 29)
        attributes([
                       "Specification-Title": "Freenet",
                       "Specification-Version": "0.7.5",
                       "Specification-Vendor": "freenetproject.org",
                       "Implementation-Title": "Freenet",
                       "Implementation-Version": "0.7.5 ${gitrev}",
                       "Implementation-Vendor": "freenetproject.org",
                   ], "common")
    }
}
jar.dependsOn buildInfo

task copyResourcesToClasses << {
    copy {
        from "${projectDir}/src/freenet/"
        into "${buildDir}/classes/main/freenet/"
        include 'freenet/l10n/*properties'
        include 'freenet/l10n/iso-*.tab'
	include 'freenet/clients/http/staticfiles/**'
	include 'dependencies.properties'
    }
}
processResources.dependsOn copyResourcesToClasses

task copyTestResourcesToClasses << {
    copy {
        from "${projectDir}/test/"
        into "${buildDir}/classes/test/"
        include 'freenet/client/filter/png/**'
        include 'freenet/client/filter/bmp/**'
        include 'freenet/client/filter/mp3/**'
        include 'freenet/crypt/ciphers/rijndael-gladman-test-data/**'
        include 'freenet/l10n/*properties'
    }
}
processTestResources.dependsOn copyResourcesToClasses
processTestResources.dependsOn copyTestResourcesToClasses

test {
    minHeapSize = "128m"
    maxHeapSize = "512m"
    // no inner class
    include 'freenet/**/*Test.class'
    exclude 'freenet/**/*$*Test.class'
    workingDir = 'build/classes/test/'
    scanForTestClasses = false
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile "org.bouncycastle:bcprov-jdk15on:1.54"
    compile "org.freenetproject:freenet-ext@jar"

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile "org.hamcrest:hamcrest-library:1.3"
}

dependencyVerification {
    // testCompile includes all of compile deps... so let's include only these
    includedConfigurations = [configurations.testCompile]
    verify = [
        'org.bouncycastle:bcprov-jdk15on:d0ae14598f9c528d2ab7bb8ed00e785a5440f692712cd362d69328aba25efb57',
        'org.freenetproject:freenet-ext:32f2b3d6beedf54137ea2f9a3ebef67666d769f0966b08cd17fd7db59ba4d79f',
        'junit:junit:59721f0805e223d84b90677887d9ff567dc534d7c502ca903c0c2b17f05c116a',
        'org.mockito:mockito-core:f97483ba0944b9fa133aa29638764ddbeadb51ec3dbc02074c58fa2caecd07fa',
        'org.hamcrest:hamcrest-library:711d64522f9ec410983bd310934296da134be4254a125080a0416ec178dfad1c',
        'org.hamcrest:hamcrest-core:66fdef91e9739348df7a096aa384a5685f4e875584cce89386a7a47251c4d8e9',
        'org.objenesis:objenesis:c5694b55d92527479382f254199b3c6b1d8780f652ad61e9ca59919887f491a8',
    ]
}

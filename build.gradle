import java.security.MessageDigest

plugins {
    id 'java'
    id 'maven-publish'
}

// Use version from Git if no version is provided
if (version == DEFAULT_VERSION) {
    version = versionFromGit
}

repositories {
    flatDir { dirs uri("${projectDir}/lib") }
    maven {
        url = 'https://mvn.freenetproject.org'
        metadataSources {
            artifact()
        }
    }
    mavenCentral()
}

dependencies {
    implementation 'org.bouncycastle:bcprov-jdk15on:1.59'
    implementation 'net.java.dev.jna:jna:4.5.2'
    implementation 'net.java.dev.jna:jna-platform:4.5.2'
    implementation 'org.freenetproject:freenet-ext:29'
    implementation 'io.pebbletemplates:pebble:3.1.5'
    // dependencies of pebble
    implementation 'org.unbescape:unbescape:1.1.6.RELEASE'
    implementation 'org.slf4j:slf4j-api:1.7.25'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:1.9.5'
    testImplementation 'org.hamcrest:hamcrest:3.0'
    testImplementation 'org.objenesis:objenesis:1.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.freenetproject'
            artifactId = 'fred'
            from components.java
        }
    }
    repositories {
        maven {
            url = 's3://mvn.freenetproject.org/'
            credentials(AwsCredentials) {
                accessKey = System.getenv('AWS_ACCESS_KEY_ID')
                secretKey = System.getenv('AWS_SECRET_ACCESS_KEY')
            }
        }
    }
}

def versionJavaFile = 'freenet/node/Version.java'

sourceSets {
    main {
        java {
            srcDirs 'src'
            include '**/*.java'
            exclude versionJavaFile
        }
        resources {
            srcDirs 'src'
            include 'freenet/l10n/*properties'
            include 'freenet/l10n/iso-*.tab'
            include 'freenet/clients/http/staticfiles/**'
            include 'freenet/clients/http/templates/**'
        }
    }
    test {
        java {
            srcDirs 'test'
            include '**/*.java'
        }
        resources {
            srcDirs 'test'
            include 'freenet/client/filter/*/**'
            include 'freenet/crypt/ciphers/rijndael-gladman-test-data/**'
            include 'freenet/l10n/*properties'
            include 'freenet/clients/http/templates/**'
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

tasks.register('versionSource', Copy) {
    inputs.property('version', version)
    from(sourceSets.main.java.srcDirs) {
        include versionJavaFile
    }
    filteringCharset = 'UTF-8'
    filter {
        line -> line.replaceAll('@custom@', inputs.properties.version as String)
    }
    into temporaryDir
}

extendSourceDirectorySet(sourceSets.main.java, 'version') {
    // Add the generated version file
    srcDirs versionSource
    include versionJavaFile
}

extendSourceDirectorySet(sourceSets.main.resources, 'dependencies') {
    // Add `dependencies.properties` from the project root
    srcDirs projectDir
    include 'dependencies.properties'
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

test {
    if (JavaVersion.current() >= JavaVersion.VERSION_1_9) {
        jvmArgs '--add-opens=java.base/java.lang=ALL-UNNAMED'
        jvmArgs '--add-opens=java.base/java.util=ALL-UNNAMED'
        jvmArgs '--add-opens=java.base/java.io=ALL-UNNAMED'
        jvmArgs '--add-opens=java.base/java.util.zip=ALL-UNNAMED'
    }
    useJUnit()
    minHeapSize = '128m'
    maxHeapSize = '512m'
    // Do not run inner classes, they are registered as part of a Suite
    include 'freenet/**/*Test.class'
    exclude 'freenet/**/*$*Test.class'
    scanForTestClasses = false
}

javadoc {
    title = "Freenet REference Daemon ${version}"
    options {
        encoding = 'UTF-8'
        source = java.sourceCompatibility
        // Ignore Javadoc linting errors
        addBooleanOption('Xdoclint:none', true)
    }
}

jar {
    archiveFileName = 'freenet.jar'
    manifest {
        attributes 'Permissions': 'all-permissions'
        attributes 'Application-Name': 'Freenet REference Daemon'
        attributes 'Required-Ext-Version': 29
        attributes 'Recommended-Ext-Version': 29
        attributes 'Compiled-With': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})"
        attributes([
                'Specification-Title'   : 'Freenet',
                'Specification-Version' : '0.7.5',
                'Specification-Vendor'  : 'freenetproject.org',
                'Implementation-Title'  : 'Freenet',
                'Implementation-Version': "0.7.5 ${version}",
                'Implementation-Vendor' : 'freenetproject.org',
        ], 'common')
    }
}

sourcesJar {
    archiveFileName = 'freenet-sources.jar'
}

javadocJar {
    archiveFileName = 'freenet-javadoc.jar'
}

tasks.register('copyRuntimeLibs', Copy) {
    description = 'Collect all runtime artifacts and dependencies.'
    group = 'Build'

    into layout.buildDirectory.dir('output')
    from configurations.runtimeClasspath
    from jar
}

assemble {
    // Add copyRuntimeLibs so `gradlew assemble` collects all build artifacts (except tar).
    // Note: jar, sourcesJar, javadocJar are added as dependency by default.
    dependsOn copyRuntimeLibs
}

tasks.register('tar', Tar) {
    description = 'Build a source release, specifically excluding the build directories and gradle wrapper files.'
    group = 'Build'

    from(project.rootDir) {
        exclude '**/build'
        exclude 'build'
        exclude '.gradle'
    }
    into 'freenet-sources'

    compression = Compression.BZIP2
    destinationDirectory = layout.buildDirectory
    archiveFileName = 'freenet-sources.tgz'

    doLast { // generate md5 checksum
        ant.checksum file: file(archiveFile)
    }
}

tasks.register('archiveDigests') {
    description = 'Display SHA-256 digests of all archives that were created.'
    group = 'Help'

    // Do not declare dependencies, we'll print whatever is available
    def artifacts = configurations.archives.artifacts

    // Run this task at the end of the build
    shouldRunAfter tasks

    // Print SHA256 digests of all available archive artifacts
    doLast {
        def sha256 = MessageDigest.getInstance('SHA-256')
        artifacts.files.files.findAll {
            it.canRead()
        }.each { file ->
            file.eachByte(1024 * 4) { buffer, len -> sha256.update(buffer, 0, len) }
            logger.lifecycle('SHA-256 of {}: {}', file.name, sha256.digest().encodeHex())
        }
    }
}

tasks.withType(AbstractArchiveTask).configureEach {
    // Ensure reproducible archives
    preserveFileTimestamps = false
    reproducibleFileOrder = true

    // Fail the build if any file would be overwritten due to duplicates
    duplicatesStrategy = DuplicatesStrategy.FAIL

    // Register the archive for displaying digests at the end of the build
    artifacts.archives archiveFile
    finalizedBy archiveDigests
}

/**
 * Try to obtain the version description from `git describe`.
 */
String getVersionFromGit() {
    try {
        def process = 'git describe --always --abbrev=4 --dirty'.execute([], project.rootDir)
        if (process.waitFor() != 0) {
            throw new IOException("exit code ${process.exitValue()}")
        }
        return process.text.trim()
    } catch (IOException e) {
        logger.warn('Cannot determine version from git: {}', e.message)
        return '@unknown@'
    }
}

/**
 * Add additional sources to a existing source directory. The additional sources can specify their own inclusion/
 * exclusion patterns independently from the existing sources.
 */
void extendSourceDirectorySet(SourceDirectorySet parent, String name, @DelegatesTo(value = SourceDirectorySet) Closure cl) {
    def child = objects.sourceDirectorySet(name, parent.name)
    cl.rehydrate(child, cl.owner, cl.thisObject).call()
    parent.source child
}
